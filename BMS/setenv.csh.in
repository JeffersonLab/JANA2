#!/bin/csh -f

# This file is used to set up ones environment to use this JANA installation
# when running csh or tcsh. It is generated automatically when JANA is
# configured and installed during the install phase. To use it, do this:
#
# source /path/to/JANA/setenv.csh
#
# This sets environment variables for some other packages for which
# support was compiled into this version of JANA. Usually, you will
# need to use the same versions of these packages, hence their inclusion
# here. If you do not want those variables to be set here, then you can
# disable it by setting the JANAONLY environment variable.

# (DY)LD_LIBRARY_PATH
# On BSD systems, DYLD_LIBRARY_PATH is used while on most others
# (including Linux) LD_LIBRARY_PATH is used. To accomodate both styles,
# we put everything in a local LIBRARY_PATH variable and then
# add that to the appropriate variable at the end of the script.
# We initialize the LIBRARY_PATH with the JANA lib directory


setenv BMS_OSNAME "@BMS_OSNAME@"
setenv JANA_INSTALL_DIR "@prefix@"
setenv JANA_HOME $JANA_INSTALL_DIR
setenv PATH ${JANA_HOME}/bin:${PATH}
set LIBRARY_PATH="${JANA_HOME}/lib"

# Add to JANA_PLUGIN_PATH or set it if it doesn't exist
if ( ! $?JANA_PLUGIN_PATH ) then
	setenv JANA_PLUGIN_PATH $JANA_HOME/lib
else
	setenv JANA_PLUGIN_PATH $JANA_HOME/lib:${JANA_PLUGIN_PATH}
endif

# Optionally set environment for support packages
if ( ! $?JANAONLY ) then

	# Xerces
	set HAVE_XERCES="@HAVE_XERCES@"
	if ( $HAVE_XERCES )then
		setenv XERCESCROOT "@XERCESCROOT@"
		set LIBRARY_PATH="${XERCESCROOT}/lib:${LIBRARY_PATH}"
	endif

	# ROOT
	set HAVE_ROOT="@HAVE_ROOT@"
	if ( $HAVE_ROOT )then
		setenv ROOTSYS "@ROOTSYS@"
		setenv PATH ${ROOTSYS}/bin:${PATH}
		set LIBRARY_PATH="${ROOTSYS}/lib:${LIBRARY_PATH}"
	endif

	# cMsg
	set HAVE_CMSG="@HAVE_CMSG@"
	if ( $HAVE_CMSG )then
		setenv CMSGROOT "@CMSGROOT@"
		setenv PATH ${CMSGROOT}/bin:${PATH}
		set LIBRARY_PATH="${CMSGROOT}/lib:${LIBRARY_PATH}"
	endif

	# CCDB
	set HAVE_CCDB="@HAVE_CCDB@"
	if ( $HAVE_CCDB )then
		setenv CCDB_HOME "@CCDB_HOME@"
		setenv PATH ${CCDB_HOME}/bin:${PATH}
		set LIBRARY_PATH="${CCDB_HOME}/lib:${LIBRARY_PATH}"
	endif

endif # JANAONLY

# Copy LIBRARY_PATH to the appropriate variable
set OS="@OS@"
if ( $OS == "Darwin" ) then
	if ( $?DYLD_LIBRARY_PATH ) then
		setenv DYLD_LIBRARY_PATH ${LIBRARY_PATH}:$DYLD_LIBRARY_PATH
	else
		setenv DYLD_LIBRARY_PATH ${LIBRARY_PATH}
	endif
else
	if ( $?LD_LIBRARY_PATH ) then
		setenv LD_LIBRARY_PATH ${LIBRARY_PATH}:$LD_LIBRARY_PATH
	else
		setenv LD_LIBRARY_PATH ${LIBRARY_PATH}
	endif
endif

